buildscript {
	ext {
		springBootVersion = "${findProperty('bootVersion') ?: bootVersion}"
		contractVersion = "${findProperty('verifierVersion') ?: verifierVersion}"
	}
	repositories {
		mavenCentral()
		mavenLocal()
		maven { url "http://repo.spring.io/snapshot" }
		maven { url "http://repo.spring.io/milestone" }
		maven { url "http://repo.spring.io/release" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		// Add contract plugin to the classpath
		classpath("org.springframework.cloud:spring-cloud-contract-gradle-plugin:${contractVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

// apply the contract plugin
apply plugin: 'spring-cloud-contract'


group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
	mavenLocal()
	maven { url "http://repo.spring.io/snapshot" }
	maven { url "http://repo.spring.io/milestone" }
	maven { url "http://repo.spring.io/release" }
}


ext {
	springCloudVersion = "${findProperty('$BOM_VERSION') ?: BOM_VERSION}"
}

dependencies {
	implementation('org.springframework.boot:spring-boot-starter-web')

	testImplementation('org.springframework.boot:spring-boot-starter-test')
	testImplementation('org.springframework.cloud:spring-cloud-starter-contract-verifier')
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

task prepareGit(type: Copy) {
	from new File(project.rootDir, "../git-repo/git")
	into 'build/gitrepo/.git'
}

copyContracts.dependsOn("prepareGit")

// Add the contract plugin setup

contracts {
	baseClassForTests = "com.example.demo.BaseClass"
	// We want to pick contracts from a Git repository
	contractDependency {
		stringNotation = "${project.group}:${project.name}:${project.version}"
	}
	/*
	We reuse the contract dependency section to set up the path
	to the folder that contains the contract definitions. In our case the
	path will be /groupId/artifactId/version/contracts
	 */
	contractRepository {
		repositoryUrl = "git://file://${project.buildDir}/gitrepo/"
	}
	// The mode can't be classpath
	contractsMode = "REMOTE"
	// Base class mappings etc.
}

/*
In this scenario we want to publish stubs to SCM whenever
the `publish` task is executed
*/
build.dependsOn("publishStubsToScm")

test.enabled = false

