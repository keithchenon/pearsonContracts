buildscript {
	repositories {
		mavenCentral()
		mavenLocal()
		maven { url "http://repo.spring.io/snapshot" }
		maven { url "http://repo.spring.io/milestone" }
		maven { url "http://repo.spring.io/release" }
	}

	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${findProperty('bootVersion') ?: bootVersion}"
		// IMPORTANT!
		classpath "au.com.dius:pact-jvm-provider-gradle_2.12:3.5.23"
	}
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

repositories {
	mavenCentral()
	mavenLocal()
	maven { url "http://repo.spring.io/snapshot" }
	maven { url "http://repo.spring.io/milestone" }
	maven { url "http://repo.spring.io/release" }
}

apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'maven-publish'
apply plugin: 'au.com.dius.pact'

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:$BOM_VERSION"
	}
}

dependencies {
	compile("org.springframework.boot:spring-boot-starter-web")
	compile("org.springframework.boot:spring-boot-starter-actuator")

	testCompile("org.springframework.cloud:spring-cloud-starter-contract-stub-runner")
	testCompile("org.springframework.cloud:spring-cloud-contract-pact")
}

test {
	testLogging {
		exceptionFormat = 'full'
	}
}

new File("${project.rootDir}/build/pacts").mkdirs()

pact {
	publish {
		pactBrokerUrl = "http://localhost:8085"
		pactDirectory = "${project.rootDir}/build/pacts"
	}
}

task convertContracts(type: JavaExec) {
	main = "org.springframework.cloud.contract.verifier.util.ToFileContractsTransformer"
	classpath = sourceSets.test.compileClasspath
	args("org.springframework.cloud.contract.verifier.spec.pact.PactContractConverter",
			"${project.rootDir}/build/pacts", "${project.rootDir}/src/test/resources/contracts")
}

test.dependsOn("convertContracts")

publishToMavenLocal.dependsOn("pactPublish")
